#!/usr/bin/perl
use strict;
use warnings;
use Data::Dumper;
#
# Create a 4.2BSD SimH image of a system which is set up to make uucp
# connections and pass mail to other 4.2BSD SimH systems.
#
# Usage: buildimg hostname:ourport remote1:server:port remote2:server:port ...
#
# Example: buildimg seismo:6345 mcvax:11.23.45.66:9000  ucbvax:www.fred.com:1800
#
# We will run our image of seismo on our machine on TCP port 6345.
# There is another uucp node called mcvax on machine 11.23.45.66 port 9000
# There is another uucp node called ucbvax on machine www.fred.com port 1800
#
# The script will build a tarball with changes to the generic SimH image,
# boot the SimH image. The SimH image will import the tarball and run a
# script to apply any final changes, e.g. file ownerships and permissions.

if (@ARGV < 1) {
  die("Usage: $0 hostname:ourport remote1:server:port remote2:server:port ...\n");
}

# Get our uucp hostname and the set of remote uucp machines
my @remotename;
my @remoteserver;
my ($hostname,$hostport)=split(/:/, shift(@ARGV));
my $remotecount= scalar(@ARGV);

foreach my $i (@ARGV) {
  my ($j, $k)= split(/:/, $i, 2);
  push(@remotename, $j);
  push(@remoteserver, $k);
}

# Remove any previous filesystem
system("rm -rf tmp");

# Make the additional filesystem areas for the customised tarball
system("rsync -a template/. tmp");

# Personalise the hostname
system("echo $hostname > tmp/etc/hostname");

# Create the ttys file
open(my $OUT, ">", "tmp/etc/ttys") || die("tmp/etc/ttys: $!");
print($OUT "console \"/etc/getty std.9600\"  unknown  on  secure\n");
foreach my $i (0 .. 7) {
  if ($i < $remotecount) {
    print($OUT "tty0$i \"/etc/getty std.9600\"   unknown off insecure\n");
  } else {
    print($OUT "tty0$i \"/etc/getty std.9600\"   unknown on  insecure\n");
  }
}
close($OUT);

# Generate the L.sys file
open($OUT, ">", "tmp/usr/lib/uucp/L.sys") || die("tmp/usr/lib/uucp/L.sys: $!");
my $i=0;
foreach my $r (@remotename) {
  print($OUT "$r Any;9 DIR 9600 tty0$i \"\" \"\" " .
		"ogin:--ogin:--ogin: uucp ssword: uucp\n"); $i++;
}
close($OUT);

# Generate the uucp.map file
open($OUT, ">", "tmp/usr/lib/uucp/uucp.map") || die("tmp/usr/lib/uucp/uucp.map: $!");
foreach my $r (@remotename) {
  print($OUT "$hostname	$r(DIRECT)\n");
}
close($OUT);

# Customise the sendmail.cf
system("sed 's/HOSTNAME/$hostname/' < template/usr/lib/sendmail.cf > tmp/usr/lib/sendmail.cf");

# Add the remote system names to the mkdirs script
my $r= "remote=\\'" . join(' ', @remotename) . "\\'";
system("echo $r > tmp/mkdirs; chmod 755 tmp/mkdirs");
system("cat template/mkdirs >> tmp/mkdirs");

# Build the tap image
system("(cd tmp; bsdtar -c --uid 0 --uname root --gid 0 --gname root -f - .) | ./mktape /dev/stdin > $hostname.tap");
system("rm -rf tmp");

# Generate the system's SimH config file
open($OUT, ">", "$hostname.ini") || die("$hostname.ini: $!");

print($OUT "# Set up eight DZ serial ports in 8-bit mode.\n");
print($OUT "# Connect some lines to remote servers\n");
print($OUT "# Connect the remaining lines to listen on port $hostport\n");
print($OUT "set dz lines=8\n");
print($OUT "set dz 8b\n");
$i=0;
foreach my $server (@remoteserver) {
  print($OUT "attach dz -m -a line=$i,Connect=$server\n"); $i++;
}
print($OUT "att dz -m -a $hostport\n");

print $OUT <<"HERE";
# Enable NAT
set xu enable
att xu nat:
#
set rq0 ra81
att rq0 $hostname.dsk
att ts $hostname.tap
set rq1 dis
set rq2 dis
set rq3 dis
set rp dis
set lpt dis
set rl dis
set tq dis
set tu dis
set tti 7b
set tto 7b
load -o boot42 0
d r10 9
d r11 0
set cpu idle=QUASIJARUS
run 2
HERE

close($OUT);
exit(0);
